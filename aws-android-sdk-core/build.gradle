apply plugin: 'java-library'

dependencies {
    api 'com.google.code.gson:gson:2.2.4'
    api 'com.google.android:android:2.3.3'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.1.1'

    // The getApi23 task puts a jar file in ./libs, so we need this.
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'joda-time:joda-time:2.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation 'org.easymock:easymock:3.1'
}

// This task is to create a symbolic link to android-23.jar
// which is used only for compilation. Once the symbolic link
// is created, AWSCore has a compile only dependency on the
// jar linked by this symbolic link.

// The minSdkVersion of AWSCore is still API Level 10. AWSCore
// uses API introduced in API Level 23 which will be executed
// only on device running an API Level of 23 and above.

// gradle task can only have one commandLine.
// The following command (is idempotent)
// removes the libs folder, creates libs folder and
// creates a symbolic link to android-23.jar.
task getApi23(type:Exec) {
    print 'Creating a symlink for Android API Level 23.'
    workingDir '.'
    commandLine 'sh', '-c', "rm -rf libs && mkdir libs && ln -s $System.env.ANDROID_HOME/platforms/android-23/android.jar libs/android-23.jar"
}

compileJava.dependsOn(getApi23)

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

